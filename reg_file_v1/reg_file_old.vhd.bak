library ieee;
use ieee.std_logic_1164.all;
use ieee.numeric_std.all;

package reg_file_pkg is
    
    constant REG_FILE_DATA_WIDTH : integer := 16;
    constant REG_FILE_ADDR_WIDTH : integer := 16;
    
    type reg_t is record
        reg_0 : std_logic_vector(REG_FILE_DATA_WIDTH-1 downto 0); -- RW
        reg_0_addr : std_logic_vector(REG_FILE_ADDR_WIDTH-1 downto 0);
        reg_0_pulse : std_logic;
        
        reg_1 : std_logic_vector(REG_FILE_DATA_WIDTH-1 downto 0); -- RW
        reg_1_addr : std_logic_vector(REG_FILE_ADDR_WIDTH-1 downto 0);
        reg_1_pulse : std_logic;
        
        reg_2 : std_logic_vector(REG_FILE_DATA_WIDTH-1 downto 0); -- RO
        reg_2_addr : std_logic_vector(REG_FILE_ADDR_WIDTH-1 downto 0);
        
        reg_3 : std_logic_vector(REG_FILE_DATA_WIDTH-1 downto 0); -- RO
        reg_3_addr : std_logic_vector(REG_FILE_ADDR_WIDTH-1 downto 0);
    end record;
    
    type transaction_state_t is (get_addr, load_reg, write_reg, read_reg);
    
end package;

library ieee;
use ieee.std_logic_1164.all;
use ieee.numeric_std.all;

library work;
use work.reg_file_pkg.all;

entity reg_file is
    port
    (
        clk : in std_logic;
        reset : in std_logic;
        
        transaction_en : in std_logic;
        rw : in std_logic; -- '0' = read, '1' = write
        rw_ready : out std_logic;
        
        reg_cs : in std_logic;
        reg_spi_clk : in std_logic;
        reg_miso : out std_logic;
        reg_mosi : in std_logic;
        
        reg_2 : in std_logic_vector(REG_FILE_DATA_WIDTH-1 downto 0);
        reg_3 : in std_logic_vector(REG_FILE_DATA_WIDTH-1 downto 0);
        
        reg_out : out reg_t        

    );
end entity;

architecture rtl of reg_file is

    component spi_slave is
        generic
        (
            constant DWIDTH : natural range 1 to 32;
            constant MSB_FIRST : natural range 0 to 1 -- 0=LSB_FIRST
        );
        port
        (
            clk : in std_logic;
            reset : in std_logic;
            enable : in std_logic;
            
            chip_select : in std_logic;
            spi_clk : in std_logic;
            spi_data_out : out std_logic;
            spi_data_in : in std_logic;
            
            din : in std_logic_vector(DWIDTH-1 downto 0);
            din_valid : in std_logic;
            din_last : in std_logic;
            din_ready : out std_logic;
            
            dout : out std_logic_vector(DWIDTH-1 downto 0);
            dout_valid : out std_logic;
            dout_last : out std_logic;
            dout_ready : in std_logic
        );
    end component;


    
    signal transaction_state : transaction_state_t;
    signal reg_int : reg_t := 
    (
        (others => '0'),
        (others => '0'),
        '0',
        (others => '0'),
        (others => '0'),
        '0',
        (others => '0'),
        (others => '0'),
        (others => '0'),
        (others => '0')
    );
    signal rw_ready_int : std_logic;
    signal address : std_logic_vector(REG_FILE_ADDR_WIDTH-1 downto 0);
    
    signal spi_slv_dout : std_logic_vector(REG_FILE_ADDR_WIDTH-1 downto 0);
    signal spi_slv_dout_valid : std_logic;
    signal spi_slv_din_valid : std_logic;
    signal spi_slv_din_ready : std_logic;
    signal spi_slv_din : std_logic_vector(REG_FILE_DATA_WIDTH-1 downto 0);
    --signal spi_slv : dout_ready;
    
    signal transaction_en_cross_0 : std_logic;
    signal transaction_en_cross_1 : std_logic;
    signal rw_cross_0 : std_logic;
    signal rw_cross_1 : std_logic;

begin

    p_domain_cross : process(clk)
    begin
        if rising_edge(clk) then
            if reset = '1' then
                transaction_en_cross_0 <= '0';
                transaction_en_cross_1 <= '0';
                
                rw_cross_0 <= '0';
                rw_cross_1 <= '0';
            else
                transaction_en_cross_0 <= transaction_en;
                transaction_en_cross_1 <= transaction_en_cross_0;
                
                rw_cross_0 <= rw;
                rw_cross_1 <= rw_cross_0;
            end if;
        end if;
    end process;
    
    reg_out <= reg_int;
    rw_ready <= rw_ready_int;
    
    reg_int.reg_0_addr <= x"0000";
    reg_int.reg_1_addr <= x"0001";
    reg_int.reg_2_addr <= x"0002";
    reg_int.reg_3_addr <= x"0003";
    
    spi_slv_din <= 
        reg_int.reg_0 when address = reg_int.reg_0_addr else
        reg_int.reg_1 when address = reg_int.reg_1_addr else
        reg_int.reg_2 when address = reg_int.reg_2_addr else
        reg_int.reg_3 when address = reg_int.reg_3_addr else
        (others => '0');
    
    reg_int.reg_0_pulse <= spi_slv_dout_valid when address = reg_int.reg_0_addr and transaction_state = write_reg else '0';
    reg_int.reg_1_pulse <= spi_slv_dout_valid when address = reg_int.reg_1_addr and transaction_state = write_reg else '0';

    reg_int.reg_0 <= spi_slv_dout when address = reg_int.reg_0_addr and transaction_state = write_reg and spi_slv_dout_valid = '1';
    reg_int.reg_1 <= spi_slv_dout when address = reg_int.reg_1_addr and transaction_state = write_reg and spi_slv_dout_valid = '1';

    reg_int.reg_2 <= reg_2;
    reg_int.reg_3 <= reg_3;

    p_transaction_sm : process(clk)
    begin
        if rising_edge(clk) then
            if reset = '1' then
                transaction_state <= get_addr;
                rw_ready_int <= '0';
                spi_slv_din_valid <= '0';
                address <= (others => '0');
            else
                case(transaction_state) is 
                    when get_addr =>
                        rw_ready_int <= '0';
                        if spi_slv_dout_valid = '1' then
                            address <= spi_slv_dout;
                            transaction_state <= load_reg;
                            spi_slv_din_valid <= '1';
                        end if;
                        
                    when load_reg =>
                        if spi_slv_din_ready = '1' then
                            spi_slv_din_valid <= '0';
                            if rw_cross_1 = '1' then
                                transaction_state <= write_reg;
                            else
                                transaction_state <= read_reg;
                            end if; 
                        end if;
                        
                    when write_reg =>
                        rw_ready_int <= '1';
                        if spi_slv_dout_valid = '1' then
                            transaction_state <= get_addr;
                        end if;
                        
                    when read_reg =>
                        rw_ready_int <= '1';
                        if spi_slv_dout_valid = '1' then
                            transaction_state <= get_addr;
                        end if;
                            
                    when others =>
                        transaction_state <= get_addr;
                        
                end case;
                
                if transaction_en_cross_1 = '0' then
                    transaction_state <= get_addr;
                end if;
                
            end if;
        end if;
    end process;

    u_spi_slave : spi_slave
        generic map
        (
            DWIDTH => REG_FILE_DATA_WIDTH,
            MSB_FIRST => 1
        )
        port map
        (
            clk => clk,
            reset => reset,
            enable => '1',
            
            chip_select => reg_cs,
            spi_clk => reg_spi_clk,
            spi_data_out => reg_miso,
            spi_data_in => reg_mosi,
            
            din => spi_slv_din,
            din_valid => spi_slv_din_valid,
            din_last => '0',
            din_ready => spi_slv_din_ready,
            
            dout => spi_slv_dout,
            dout_valid => spi_slv_dout_valid,
            dout_last => open,
            dout_ready => '1'
        );

end rtl;


















